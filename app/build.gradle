apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    // Copied from tachiyomi
    // Git is needed in your system PATH for these commands to work.
    // If it's not installed, you can return a random value as a workaround
    getCommitCount = {
        return 'git rev-list --count HEAD'.execute().text.trim()
        // return "1"
    }
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'com.github.doomsdayrs.apps.shosetsu'
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 26
        versionName "v1.1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix = '-release'
            multiDexEnabled = true
        }
        debug {
            versionNameSuffix "-r${getCommitCount()}"
            applicationIdSuffix '.debug'
            debuggable true
        }
    }
    compileOptions {
        targetCompatibility = "1.8"
        sourceCompatibility = '1.8'
    }
    buildToolsVersion = '28.0.3'
}
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def appName = "shosetsu"
        outputFileName = appName + "-${variant.versionName}.apk"
    }
}
android {
    lintOptions {
        abortOnError false
    }
}


dependencies {
    implementation project (':backend')

    //   implementation 'com.graphql-java:graphql-java:2020-01-17T02-41-10-578985f'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //noinspection GradleCompatible
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.work:work-runtime:2.3.2'
    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    // Annotations
    implementation 'org.jetbrains:annotations:18.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    // Core libraries
    implementation 'com.github.doomsdayrs:shosetsu-services:1.0.0-rc7'

    // Image loading
    implementation 'com.squareup.picasso:picasso:2.71828'

    //TODO GITHUB sign in to save backup data to cloud
    //implementation 'com.github.kohsuke:github-api:github-api-1.95'

    // App updater
    implementation 'com.github.javiersantos:AppUpdater:2.7'

    // Markdown view
    implementation 'us.feras.mdv:markdownview:1.1.0'

    // Time control
    implementation 'joda-time:joda-time:2.10.5'

    // Choosing backup files
    implementation 'com.vincent.filepicker:MultiTypeFilePicker:1.0.8'


    // TODO Implement readerview provided by Mozilla
    //implementation "org.mozilla.components:feature-readerview:1.0.0"
    //implementation 'org.mozilla.components:support-base:1.0.0'

    // Cloud flare calculator
    implementation 'com.zhkrb.cloudflare-scrape-android:scrape-rhino:0.1.1'


    implementation 'androidx.core:core-ktx:1.3.0-alpha02'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.70"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.70"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    // Async controller
    implementation 'com.zsoltsafrany:needle:1.0.0'

    // Showcase
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.4'

    // A cuter view
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'

    // Error logging to email
    implementation "ch.acra:acra-mail:5.1.3"
    implementation "ch.acra:acra-dialog:5.1.3"

    // Kodein injection; oneday
    //implementation 'org.kodein.di:kodein-di-generic-jvm:6.5.0'

    // Conductor
    implementation 'com.bluelinelabs:conductor:3.0.0-rc1'
    implementation 'com.bluelinelabs:conductor-support:3.0.0-rc1'

    // FastScroll
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'
}
