apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

ext {
    kodein_version = "6.5.5"
    conductor_version = '3.0.0'
    conductor_version_support = "3.0.0-rc2"

    // Copied from tachiyomi
    // Git is needed in your system PATH for these commands to work.
    // If it's not installed, you can return a random value as a workaround
    getCommitCount = {
        'git rev-list --count HEAD'.execute().text.trim()
        // return "1"
    }
    getLatestCommitMsg = {
        'git log -1'.execute().text.trim()
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'com.github.doomsdayrs.apps.shosetsu'
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 26
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix ""
            multiDexEnabled true
        }
        debug {
            versionNameSuffix "-${getCommitCount()}"
            applicationIdSuffix '.debug'
            debuggable true
        }
    }
    flavorDimensions "default"
    productFlavors {
        playstore {
            // play store will be in this
            applicationId "app.shosetsu.android"
            applicationIdSuffix ".play"
            versionNameSuffix "-play"
        }
        uptodown {
            applicationIdSuffix ".uptodown"
            versionNameSuffix "-uptodown"
        }
        fdroid {
            applicationIdSuffix ".fdroid"
            versionNameSuffix "-fdroid"
        }
        standard {
            isDefault = true
        }
    }
    compileOptions {
        targetCompatibility '1.8'
        sourceCompatibility '1.8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildToolsVersion = '29.0.3'
}

android.applicationVariants.all { variant ->

    variant.outputs.all {
        def appName = 'shosetsu'
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        def flavorName = variant.flavorName
        def buildType = variant.buildType.name
        def variantName = variant.name
        def gitCount = getCommitCount()

        println("==== " + flavorName)
        println(appName)
        println(versionName)
        println(versionCode)
        println(buildType)
        println(variantName)
        println()

        if (buildType.toString() == "debug" && flavorName.toString() == "standard") {
            outputFileName = "${appName}-${gitCount}.apk"
        } else outputFileName = "${appName}-${versionName}.apk"
    }
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Google view things
    implementation 'com.google.android.material:material:1.2.1'

    // Androidx
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.recyclerview:recyclerview-selection:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.biometric:biometric:1.0.1'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'

    // - Life Cycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation project(path: ':common')

    // Test classes
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Annotations
    implementation 'org.jetbrains:annotations:18.0.0'

    // Core libraries
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'com.github.shosetsuorg:kotlin-lib:v1.0.0-rc53'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    // Image loading
    implementation 'com.squareup.picasso:picasso:2.71828'

    //TODO GITHUB sign in to save backup data to cloud
    //implementation 'com.github.kohsuke:github-api:github-api-1.95'

    // Markdown view
    implementation 'us.feras.mdv:markdownview:1.1.0'

    // Time control
    implementation 'joda-time:joda-time:2.10.5'

    // Choosing backup files
    implementation 'com.vincent.filepicker:MultiTypeFilePicker:1.0.8'

    // TODO Implement readerview provided by Mozilla
    //implementation "org.mozilla.components:feature-readerview:1.0.0"
    //implementation 'org.mozilla.components:support-base:1.0.0'

    // Cloud flare calculator
    implementation 'com.zhkrb.cloudflare-scrape-android:scrape-webview:0.0.3'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Kotlin libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.21"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.21"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    // Async controller
    implementation 'com.zsoltsafrany:needle:1.0.0'

    // Showcase
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.4'

    // A cuter view
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'

    // Error logging
    def acra_version = '5.7.0'
    implementation "ch.acra:acra-http:$acra_version"
    implementation "ch.acra:acra-mail:$acra_version"
    implementation "ch.acra:acra-dialog:$acra_version"

    // Conductor
    implementation "com.bluelinelabs:conductor:$conductor_version"
    implementation "com.bluelinelabs:conductor-support:$conductor_version_support"
    implementation "com.bluelinelabs:conductor-androidx-transition:$conductor_version"
    implementation "com.bluelinelabs:conductor-archlifecycle:$conductor_version"

    // FastScroll
    implementation 'com.github.turing-tech:MaterialScrollBar:13.3.4'

    // Material Intro https://github.com/heinrichreimer/material-intro#standard-slide-simpleslide
    implementation "com.heinrichreimersoftware:material-intro:2.0.0"

    // Color Picker
    implementation "com.github.skydoves:colorpickerview:2.1.6"

    // Seek bar
    implementation 'com.xw.repo:bubbleseekbar:3.20'

    // Room
    def room_version = "2.2.6"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Fast Adapter
    def latestFastAdapterRelease = "5.3.2"

    implementation "com.mikepenz:fastadapter:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-expandable:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-binding:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-diff:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-drag:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-paged:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-scroll:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-swipe:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-ui:${latestFastAdapterRelease}"
    implementation "com.mikepenz:fastadapter-extensions-utils:${latestFastAdapterRelease}"

    // Guava cache
    implementation 'com.google.guava:guava:30.0-android'

    // kode-in
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-core:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-x:$kodein_version"

    // KTX
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation "androidx.collection:collection-ktx:1.1.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.4.2"



    // Roomigrant
    implementation 'com.github.MatrixDev.Roomigrant:RoomigrantLib:0.2.0'
    kapt 'com.github.MatrixDev.Roomigrant:RoomigrantCompiler:0.2.0'
}